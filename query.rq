#
# List of all cheeses with ripeness between 5 and 20 days
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?label ?duration 
WHERE {
    ?cheese :hasRipening/:hasRipeningDuration ?duration.

    OPTIONAL { ?cheese rdfs:label ?label }
    FILTER (?duration > 5 && ?duration < 20)
}
ORDER BY ?label

#
# List of all protected cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX food-upper: <http://w3id.org/food/ontology/disciplinare-upper/>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?label ?protectedname ?namelabel
WHERE {
    ?cheese a/rdfs:label "ProtectedCheese"@en.
    ?cheese food-upper:haDenominazione ?protectedname

    OPTIONAL { ?cheese rdfs:label ?label }
    OPTIONAL { ?protectedname rdfs:label ?namelabel }
}
ORDER BY ?label

#
# List of all pit cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?cheeselabel ?pit ?pitlabel
WHERE {
    ?cheese (:hasRipening|:hasAging)/:isLocatedInEnvironment ?pit.
    ?pit a/rdfs:label "Pit"@en.

    OPTIONAL { ?pit rdfs:label ?pitlabel }
    OPTIONAL { ?cheese rdfs:label ?cheeselabel }
}
ORDER BY ?cheeselabel

#
# List of all cheeses made with raw milk
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?cheeselabel ?milk ?milklabel
WHERE {
    ?cheese :isMadeWithMilk ?milk.
    ?milk a :RawMilk.

    OPTIONAL { ?milk rdfs:label ?milklabel }
    OPTIONAL { ?cheese rdfs:label ?cheeselabel }
}
ORDER BY ?cheeselabel

#
# List of all cheeses made with Cervia salt
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?label
WHERE {
    ?cheese :isMadeWithRawMaterial/rdfs:label "Sale di Cervia"@it.
    
    OPTIONAL { ?cheese rdfs:label ?label }
}
ORDER BY ?label

#
# List of all cheeses along with the animal milk used for producing them
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?cheeselabel ?milk ?milklabel ?animal
WHERE {
    ?cheese :isMadeWithMilk ?milk.
    { 
        ?milk a/rdfs:label "CowMilk"@en.
        VALUES ?animal { "Formaggio di mucca" }
    }
    UNION
    { 
        ?milk a/rdfs:label "SheepMilk"@en.
        VALUES ?animal { "Formaggio di pecora" }
    }
    UNION
    { 
        ?milk a/rdfs:label "GoatMilk"@en.
        VALUES ?animal { "Formaggio di capra" }
    }
    UNION
    { 
        ?milk a/rdfs:label "BuffaloMilk"@en.
        VALUES ?animal { "Formaggio di bufala" }
    }

    OPTIONAL { ?milk rdfs:label ?milklabel }
    OPTIONAL { ?cheese rdfs:label ?cheeselabel }
}
ORDER BY ?animal

#
# List of all high-digeribility cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT DISTINCT ?cheese ?label
WHERE {
    { ?cheese :isMadeWithRawMaterial/a :VegetalRennet. }
    UNION
    { ?cheese :isMadeWithMilk/a/rdfs:label "SkimmedMilk"@en. }
    
    OPTIONAL { ?cheese rdfs:label ?label }
}
ORDER BY ?label

#
# List of all soft or semi-soft cheeses which are moldy cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?label ?type
WHERE {
    { ?cheese a/rdfs:label "SoftCheese"@en. }
    UNION
    { ?cheese a/rdfs:label "SemiSoftCheese"@en. }
    {
        ?cheese a/rdfs:label "BlueCheese"@en.
        VALUES ?type { "Formaggio erborinato" }
    }
    UNION
    {
        ?cheese a/rdfs:label "SmearRipenedCheese"@en.
        VALUES ?type { "Formaggio a crosta lavata" }
    }
    UNION
    {
        ?cheese a/rdfs:label "SoftRipenedCheese"@en.
        VALUES ?type { "Formaggio a crosta fiorita" }
    }
    
    OPTIONAL { ?cheese rdfs:label ?label }
}
ORDER BY ?type

#
# List of all cheeses made from other cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT DISTINCT ?cheese ?label
WHERE {
    ?cheese :isMadeWithRawMaterial+/a/rdfs:label "Cheese"@en
    
    OPTIONAL { ?cheese rdfs:label ?label }
}
ORDER BY ?label

#
# List all Mambelli's cheeses
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?label
WHERE {
    ?cheese a/rdfs:label "Cheese"@en.
  
    FILTER(REGEX(STR(?cheese), "Mambelli"))
  
    OPTIONAL {
        ?cheese rdfs:label ?label.

        FILTER(REGEX(?label, "Mambelli"))
    }
}
ORDER BY ?label

#
# List the number of cheeses produced in a given city for all cities along with the region containing them
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT DISTINCT ?city ?region (COUNT(?city) AS ?count)
WHERE {
    ?cheese :isProducedIn ?city.
  
    SERVICE <https://dbpedia.org/sparql> {
        ?region dbo:type dbr:Regions_of_Italy.
        ?city dbo:region ?region.
    }
}
GROUP BY ?city ?region
ORDER BY DESC(COUNT(?city))

#
# List all protected cheeses for each italian region
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX food-upper: <http://w3id.org/food/ontology/disciplinare-upper/>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?region ?cheese ?cheeselabel ?name ?namelabel
WHERE {
    ?cheese :isProducedIn ?city.
    ?cheese a/rdfs:label "ProtectedCheese"@en.
    ?cheese food-upper:haDenominazione ?name.
  
    SERVICE <https://dbpedia.org/sparql> {
        ?region dbo:type dbr:Regions_of_Italy.
        ?city dbo:region ?region.
    }
  
    OPTIONAL { ?cheese rdfs:label ?cheeselabel }
    OPTIONAL { ?name rdfs:label ?namelabel }
}
ORDER BY ?cheeselabel

#
# List all cheeses aged in pits which are located in the Forlì-Cesena province. 
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

SELECT ?cheese ?cheeselabel ?city ?citylabel ?pit ?pitlabel
WHERE {
    ?cheese :hasAging ?aging.
    ?aging :hasTakenPlaceIn ?city.
    ?aging :isLocatedInEnvironment ?pit.
    ?pit a/rdfs:label "Pit"@en.
  
    SERVICE <https://dbpedia.org/sparql> {
        ?city dbo:province dbr:Province_of_Forlì-Cesena.
    
        OPTIONAL {
            ?city rdfs:label ?citylabel.
        
            FILTER(LANG(?citylabel) = "it")
        }
    }
  
    OPTIONAL { ?cheese rdfs:label ?cheeselabel }
    OPTIONAL { ?pit rdfs:label ?pitlabel }
}
ORDER BY ?citylabel

#
# Check whether it exists some cheeses which are edible for vegetarians.
#

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX : <https://github.com/nicolasfara/cheese-ontology/>

ASK
WHERE {
    ?cheese a/rdfs:label "Cheese"@en.
    FILTER NOT EXISTS { ?cheese :isMadeWithRawMaterial/a :AnimalRennet. }
}
